
plugins {
  id 'org.springframework.boot' version '2.4.2'
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id 'io.franzbecker.gradle-lombok' version '3.0.0'
  id 'java'
  id 'idea'
  id 'com.avast.gradle.docker-compose' version '0.14.0'
  id 'com.google.cloud.tools.jib' version '2.7.1'

  id 'org.unbroken-dome.helm' version '1.4.0'
  id 'org.unbroken-dome.helm-publish' version '1.4.0'
  id 'org.unbroken-dome.helm-releases' version '1.4.0'

  id 'se.patrikerdes.use-latest-versions' version '0.2.15'
  id 'com.github.ben-manes.versions' version '0.36.0'

  id 'maven-publish'
}

ext {
  jaxbVersion = '2.3.1'
  jaxbCoreVersion = '2.3.0.1'
  okhttpVersion = '3.11.0'
  springCloudVersion = '2020.0.1'
  tag = 'k8s'
  chartRepoUrl = 'http://chartmuseum.default.svc.cluster.local/'
}

group = 'com.gregturnquist.learningspringboot'

repositories {
  mavenCentral()
  maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
  // implementation platform('org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE')
  // enforce versions defined in bom
  implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.cloud:spring-cloud-starter-config'
  implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
  implementation "com.sun.xml.bind:jaxb-core:${jaxbCoreVersion}"
  implementation "javax.xml.bind:jaxb-api:${jaxbVersion}"
  implementation "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
  implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
}

sourceCompatibility = 11
targetCompatibility = 11

lombok {
  version = '1.18.10'
  sha256 = ""
}

repositories {
  jcenter()
}

springBoot {
    buildInfo {
        properties {
            artifact = "${rootProject.name}"
            group = "${project.group}"
            name = "${rootProject.name}"
            version = "${project.version}"
        }
    }
}

bootJar {
  baseName = 'lsb2-eureka-server'
  layered()
  excludeDevtools = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.gregturnquist.learningspringboot'
            from components.java
        }
    }
}

tasks.jib.dependsOn tasks.check
jib {
    from {
        image = 'gcr.io/distroless/java:11'
    }
    to {
      image = 'jghamburg/lsb2-eureka-server'
      tags = ["latest", project.version]
      credHelper = 'osxkeychain'
    }
    extraDirectories {
        paths = file('./ssl')
    }
    container {
      jvmFlags = ['-Djava.security.egd=file:/dev/./urandom', '-Xmx128m', '-Xdebug']
      mainClass = 'com.greglturnquist.learningspringboot.LearningSpringBootEurekaServer'
      args = []
      ports = ['8080']
    }
}
helm {
    // The global lint configuration applies to all charts
    lint {
        // treat linter warnings as errors (failing the build)
        strict = true
    }

    publishing {
        repositories {
            chartMuseum('localcharts') {
                url = uri('http://chartmuseum-chartmuseum.default.svc.cluster.local:8080')
            }
        }
    }

    filtering {
        values.put 'imageRepository', jib.to.image
        values.put 'imageTag', jib.to.tags.first()
        values.put 'appVersion', "${project.version}"
    }

    charts {
        main {
          chartVersion = "${project.version}"
          sourceDir = file('src/main/helm/lsb2-eureka-server')
        }
    }

    releases {
      eurekaserver {
        valuesDir 'src/main/helm'
        forTarget('local') {
          from chart('main')
          installDependsOn 'jibDockerBuild'
          values.put 'imageTag', 'latest'
          values.put 'appVersion', "${project.version}"
        }
        forTarget('nonlive') {
          from chart 'main'
          installDependsOn 'jib'
        }
      }
    }
    releaseTargets {
        local {
//            kubeContext = 'docker-for-desktop'
        }
        nonlive {
//            kubeContext = 'aws'
//            atomic = true
        }
    }

}
/*
 * docker-compose plugin configuration.
 * Details on https://github.com/avast/gradle-docker-compose-plugin
 */
dockerCompose {
  useComposeFiles = ['docker/env/local.yml'] // like 'docker-compose -f <file>'; default is empty
  buildBeforeUp = true // performs 'docker-compose build' before calling the 'up' command; default is true

  waitForTcpPorts = true // turns off the waiting for exposed TCP ports opening
  captureContainersOutput = true // if true, prints output of all containers to Gradle output - very useful for debugging; default is false

  stopContainers = true // doesn't call `docker-compose down` - see below the paragraph about reconnecting
  removeContainers = true
  removeOrphans = false // removes containers for services not defined in the Compose file

  environment.put 'TAG', version
}

composeBuild.dependsOn bootJar
composeUp.dependsOn composeBuild
composePush.dependsOn composeBuild

task semanticRelease (type: Exec){
  workingDir '.'
  commandLine 'semantic-release', '-b', 'master', '--no-ci'
}

task verifySemanticRelease (type: Exec){
  workingDir '.'
  commandLine 'semantic-release', '-b', 'master'
}
